LOAD "pdbs/OR5/OR5P3.upright.pdb"

BRIDGE %45.51 %6.55
MEASURE %45.51 "OD1" %6.55 "OH" &d6
MEASURE %45.51 "OD2" %6.55 "OH" &d6a
IF &d6a < &d6 THEN LET &d6 = &d6a
ECHO "Old 45-6 Distance: " &d6
MEASURE %5.58 "OH" %7.53 "OH" &d57
ECHO "Old 5-7 Distance: " &d57


# Get TMR6 and EXR2 bridged.
PTALIGN @45.51 @6.55 @6.48 @axis &angle
ROTATE @axis -5.3 %6.48 %6.28 %6.58
MOVEREL %6.28 %6.58 [0,3.0,0]

# Trip switch and CGSHL histidine.
ATOMTO %6.48 "OH" @3.43
ATOMTO %6.47 "CZ" @3.37
ATOMTO %6.40 "NE2" @5.68

# Outward tilt of entire TMR6.
LET @axis = @7.53 - @5.58
ROTATE @axis -10 %6.55 %6.28 %6.58
LET @axis = @2.40 - @5.58
ROTATE @axis -5 %6.55 %6.28 %6.58
LET @axis = @3.38 - @6.48
ROTATE @axis -6 %6.49 %6.28 %6.49

# Slight tilt of TMR5 away from TMR3.
LET @axis = @2.40 - @5.58
ROTATE @axis -0.3 %5.32 %5.32 %5.68

# Move TMR5 closer to TMR7.
LET &magn = @axis
LET &mult = 1.5 / &magn
LET @axis *= &mult
LET @axis.x += 3.3
LET @axis.z -= 2.0
MOVEREL %5.32 %5.68 @axis

# Tilt entire TMR5 toward TMR7.
PTALIGN @5.68 @7.53 @5.32 @axis &angle
ROTATE @axis 5 %5.32 %5.32 %5.68

# Slight bend of TMR5.
PTALIGN @5.68 @7.53 @5.50 @axis &angle
ROTATE @axis 7 %5.50 %5.32 %5.68

# TMR7 to make room for TMR6.
PTALIGN @45.54 @7.31 @7.49 @axis &angle
ROTATE @axis 6 %7.49 %7.31 %7.49
PTALIGN @5.36 @7.31 @7.49 @axis &angle
ROTATE @axis 7 %7.49 %7.31 %7.49

# TMR7 away from TMR2.
PTALIGN @2.43 @7.53 @7.49 @axis &angle
ROTATE @axis 5 %7.49 %7.49 %7.56

# TMR7 away from EXR2.
PTALIGN @45.57 @7.31 @7.49 @axis &angle
ROTATE @axis 3 %7.49 %7.31 %7.49

# TMR1 tilt.
LET @axis = @2.38 - @1.57
ROTATE @axis -7 %1.58 %1.28 %1.58


BRIDGE %45.51 %6.55
MEASURE %45.51 "OD1" %6.55 "OH" &d6
MEASURE %45.51 "OD2" %6.55 "OH" &d6a
IF &d6a < &d6 THEN LET &d6 = &d6a
ECHO "New 45-6 Distance: " &d6
ATOMTO %7.53 "OH" @5.58
BRIDGE %5.58 %7.53
MEASURE %5.58 "OH" %7.53 "OH" &d57
ECHO "New 5-7 Distance: " &d57


DELETE 1 %1.27
DELETE %1.59 %2.37
DELETE %2.68 %3.20
DELETE %3.57 %4.38
DELETE %4.66 %45.45
DELETE %45.57 %5.31
DELETE %5.69 %6.27
DELETE %6.60 %7.30
DELETE %7.57 9999

MINC
MINC
ECHO "Total clashes: " ~
INTC ~
ECHO ", worst between " ~
WORST ~
ECHO "."

SAVE "pdbs/OR5/OR5P3.bound.pdb"
