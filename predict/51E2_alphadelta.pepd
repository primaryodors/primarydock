# Pepteditor script for deriving active-state conformational changes from cryo-EM model vs. AlphaFold.
#

# Load the inactive protein.
LOAD "pdbs/OR51/OR51E2.upright.pdb"

ECHO $SEQUENCE
ECHO ""

LET %i = 1
LET %lastresno = 0

_loop13:
LET @CA = %i
IF @CA.x == 0 AND @CA.y == 0 AND @CA.z == 0 THEN GOTO _nope18
    LET @ica[%i] = @CA
    LET %lastresno = %i
_nope18:
LET %i ++
IF %i <= %SEQLEN GOTO _loop13


# Load the active protein.
LOAD "pdbs/OR51/OR51E2.8f76.pdb"

ECHO %SEQLEN
ECHO $SEQUENCE
ECHO ""

UPRIGHT


LET %i = 1

_loop35:
LET @CA = %i
IF @CA.x == 0 AND @CA.y == 0 AND @CA.z == 0 GOTO _nope40
    LET @aca[%i] = @CA
_nope40:
LET %i ++
IF %i <= %lastresno GOTO _loop35

LET %iter = 0
_loop_iter:


# For every resno that has an entry in both arrays,
# find the Cartesian delta between CA locations.
# Then average the deltas and transform the active
# CA locations by that delta.

LET @delta = [0,0,0]
LET @origin = [0,0,0]
LET %divisor = 0
LET %i = 1

_loop51:
IF %i >= %TMR6.s AND %i <= %TMR6.e GOTO _nope61
IF @ica[%i].x == 0 AND @ica[%i].y == 0 AND @ica[%i].z == 0 GOTO _nope61
IF @aca[%i].x == 0 AND @aca[%i].y == 0 AND @aca[%i].z == 0 GOTO _nope61

LET @diff = @ica[%i] - @aca[%i]
LET @delta += @diff
LET @origin += @ica[%i]
LET %divisor ++

_nope61:
LET %i ++
IF %i <= %lastresno GOTO _loop51

IF %divisor == 0 DIE "No matching resnos between proteins."
LET @delta /= %divisor
LET @origin /= %divisor
ECHO "Whole active protein to be transformed by:" @delta

LET %i = 1

_loop70:
IF @aca[%i].x != 0 OR @aca[%i].y != 0 OR @aca[%i].z != 0 LET @aca[%i] += @delta
LET %i ++
IF %i <= %lastresno GOTO _loop70


# Now obtain the average y-axis rotational angle to align the active
# config residues to the inactive.

LET @normal = [0,1,0]
LET &angle = 0

LET &theta = 0
LET %divisor = 0

LET %i = 1

_loop89:
IF %i >= %TMR6.s AND %i <= %TMR6.e GOTO _nope104
IF @aca[%i].x == 0 AND @aca[%i].y == 0 AND @aca[%i].z == 0 GOTO _nope104
IF @ica[%i].x == 0 AND @ica[%i].y == 0 AND @ica[%i].z == 0 GOTO _nope104

LET @lac = @aca[%i]
LET @lic = @ica[%i]
LET @lac.y = @origin.y
LET @lic.y = @origin.y

PTALIGN @lac @lic @origin @normal &angle
IF @normal.y < 0 LET &angle *= -1

LET &theta += &angle
LET %divisor ++

_nope104:
LET %i ++
IF %i <= %lastresno GOTO _loop89

LET &theta /= %divisor
LET &theta *= -1
ECHO "Active protein to be rotated by " &theta " degrees about the +Y axis."


# Now do the rotations.
LET @normal = [0,1,0]
LET %i = 1

_loop116:
IF @aca[%i].x == 0 AND @aca[%i].y == 0 AND @aca[%i].z == 0 GOTO _nope119
LET @pt = @aca[%i]
PTROTATE @aca[%i] @origin @normal &theta @pt
LET @aca[%i] = @pt
_nope119:
LET %i ++
IF %i <= %lastresno GOTO _loop116


# LET %iter ++
# IF %iter < 100 GOTO _loop_iter


# Finally, output the Cartesian deltas for all residues.
ECHO

LET %i = 1
_loop126:
IF @aca[%i].x == 0 AND @aca[%i].y == 0 AND @aca[%i].z == 0 GOTO _nope130
LET @delta = @aca[%i] - @ica[%i]
ECHO %i ": " @delta
_nope130:
LET %i ++
IF %i <= %lastresno GOTO _loop126

