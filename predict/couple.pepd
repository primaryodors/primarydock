
IF $arg2 = "" OR $arg3 = "" DIE "Usage: bin/pepteditor predict/couple.pepd path/to/receptor.pdb GPROT_ID"

LET $rcpfn = $arg2
LET $gprot = $arg3



IF $arg3 != "hGNAL" GOTO not_Golf

STRAND G
LOAD "pdbs/Gprot/mTAAR9-hGNAL.pdb" A
STRAND C
LOAD "pdbs/Gprot/mTAAR9-hGNAL.pdb" R
STRAND I
LOAD "pdbs/mTAAR9.pdb"
GOTO all_loaded


not_Golf:

IF $arg3 != "hGNAS2" GOTO not_Gas

STRAND G
LOAD "pdbs/Gprot/hOR51E2-hGNAS2.pdb" A
STRAND C
LOAD "pdbs/Gprot/hOR51E2-hGNAS2.pdb" R
STRAND I
LOAD "pdbs/OR51/OR51E2.upright.pdb"
GOTO all_loaded


not_Gas:

IF $arg3 != "hGNAQ" GOTO not_Gaq

STRAND G
LOAD "pdbs/Gprot/hAGTR1-hGNAQ.pdb" A
STRAND C
LOAD "pdbs/Gprot/hAGTR1-hGNAQ.pdb" R
STRAND I
LOAD "pdbs/hAGTR1.pdb"
GOTO all_loaded


not_Gaq:

IF $arg3 != "hGNAO1" DIE "Unknown G protein " $arg3

STRAND G
LOAD "pdbs/Gprot/rNTR1-hGNAO1.pdb" A
STRAND C
LOAD "pdbs/Gprot/rNTR1-hGNAO1.pdb" R
STRAND I
LOAD "pdbs/rNTR1.pdb"
GOTO all_loaded


all_loaded:

STRAND A
LOAD $rcpfn
HOMOLOGY C I
GOSUB custom6

UNCHAIN C
UNCHAIN I
STRAND G
RENUMBER 1 999 500

SAVE "tmp/coupled.pdb" EXIT


custom6:

LET %655 = %A.6.50 + 5
LET $655 = $SEQUENCEA.%655,1
LET %4551 = %A.45.50 + 1
LET $4551 = $SEQUENCEA.%4551,1

IF $655 != "Y" GOTO not_Y655_DE4551
IF $4551 != "D" AND $4551 != "E" GOTO not_Y655_DE4551

LET @655 = %655
LET @4551 = %4551
LET @r = @4551 - @655
LET &r = @r
# ECHO @655 " and " @4551 " are " &r " A apart."

IF &r < 9 GOTO already_Y655_DE4551

LET &d = &r - 9
LET &rat = &d / &r
LET @d = @r
LET @d *= &rat

# ECHO @r @d

LET @new = @655 + @d
LET %628 = %A.6.50 - 22
LET %648 = %A.6.50 - 2
LET %660 = %A.6.50 + 10
ALIGN %628 %660 1 %648 KEEP %655 @new

already_Y655_DE4551:
BRIDGE %655 %4551


not_Y655_DE4551:

LET %659 = %A.6.50 + 9
LET $659 = $SEQUENCEA.%659,1

IF $659 = "R" LET $atom = "CZ"
ELSE IF $659 = "K" LET $atom = "NZ"
ELSE GOTO not_KR659

ECHO $659

LET %539 = %A.5.50 - 11
LET %4552 = %A.45.50 + 2

LET @539 = %539
LET @4552 = %4552
LET @target = @539 + @4552
LET @target /= 2

ATOMTO %659 $atom @target

LET %651 = %A.6.50 + 1
LET @651 = %651
LET %4553 = %A.45.50 + 3

ATOMTO %4553 EXTENT @651


not_KR659:

RETURN


avoid_gprot_clash:

STRAND G

LET %penult = %SEQLENG - 2
LET @penult = %penult

STRAND A

LET %533 = %A.5.50 - 17
LET %628 = %A.6.50 - 22
LET %659 = %A.6.50 + 9

LET @5650 = %A.56.50
LET @dir = @5650 - @penult
LET &nrg = 0
CTNRG G A %533 %659 &nrg @dir


RETURN
